Updates to SYN package for Covid patients and WHO demo
GT.M 03-MAR-2021 17:25:11
SYNDHP63
SYNDHP63 ;DHP/ART -  Write Lab Tests to VistA ;2019-01-31  10:43 AM
 ;;0.2;VISTA SYN DATA LOADER;;Feb 07, 2019;Build 10
 ;;Original routine authored by Andrew Thompson & Ferdinand Frankson of DXC Technology 2017-2018
 ;
 QUIT
 ;
 ; -------- Create lab test for a patient
 ;
LABADD(RETSTA,DHPPAT,DHPLOC,DHPTEST,DHPRSLT,DHPRSDT,DHPLOINC) ;Create lab test
 ; This is a wrapper to setup a call to the ISI Lab Import processing
 ;
 ; Input:
 ;   DHPPAT  - Patient ICN (required)
 ;   DHPLOC  - Location Name (required)
 ;   DHPTEST - Test name (required if no valid LOINC code passed)
 ;   DHPRSLT - Test result value (required)
 ;   DHPRSDT - Test result date (required, HL7 format)
 ;   DHPLOINC - LOINC code
 ;
 ; Output:   RETSTA
 ;  1 - success
 ; -1 - failure -1^message
 ;
 N PATDFN,PATSSN,LABARRAY,RC,DHPRC,LOINCIEN,TESTIEN,LABTEST,SAVEIO
 ;
 S RETSTA("LABINPUT","DHPPAT")=DHPPAT
 S RETSTA("LABINPUT","DHPLOC")=DHPLOC
 S RETSTA("LABINPUT","DHPTEST")=DHPTEST
 S RETSTA("LABINPUT","DHPRSLT")=DHPRSLT
 S RETSTA("LABINPUT","DHPRSDT")=DHPRSDT
 S RETSTA("LABINPUT","DHPLOINC")=DHPLOINC
 ;
 I $G(DHPPAT)="" S RETSTA="-1^Missing patient identifier." QUIT
 I $G(DHPLOC)="" S RETSTA="-1^Missing location." QUIT
 I $G(DHPLOINC)="",$G(DHPTEST)="" S RETSTA="-1^Missing lab test name." QUIT
 I $G(DHPRSLT)="" S RETSTA="-1^Missing lab test result value." QUIT
 I $G(DHPRSDT)="" S RETSTA="-1^Missing lab test result date/time." QUIT
 ;
 I '$D(^DPT("AFICN",DHPPAT)) S RETSTA="-1^Patient not recognised." QUIT
 S PATDFN=$O(^DPT("AFICN",DHPPAT,""))
 S PATSSN=$$GET1^DIQ(2,PATDFN_",",.09,"I") ;patient SSN
 I PATSSN="" S RETSTA="-1^Patient does not have an SSN." QUIT
 ;
 ; -- Check lab test date/time --
 S LABDTTM=$$HL7TFM^XLFDT(DHPRSDT)
 I $P(LABDTTM,".",2)="" S RETSTA="-1^Missing time for result date/time." QUIT
 ;chop off seconds
 S LABTM=$E($P(LABDTTM,".",2),1,4)
 S $P(LABDTTM,".",2)=LABTM
 S LABDTTM=$$HL7TFM^XLFDT($$FMTHL7^XLFDT(LABDTTM)) ;drop any trailing 0's in time
 ;
 ;Get lab test name for LOINC code
 I $G(DHPLOINC)'="" D
 . S LOINCIEN=$O(^LAB(95.3,"B",$P(DHPLOINC,"-",1),""))
 . QUIT:LOINCIEN=""
 . S TESTIEN=$O(^LAB(60,"AF",LOINCIEN,""))
 . QUIT:TESTIEN=""
 . S LABTEST=$$GET1^DIQ(60,TESTIEN_",",.01)
 I $G(LABTEST)="" S LABTEST=$$UP^XLFSTR(DHPTEST)
 ;I $G(LABTEST)="" S LABTEST=DHPTEST
 I $G(LABTEST)="" S RETSTA="-1^Cannot determine lab test name." QUIT
 ;
 S LABARRAY("PAT_SSN")=PATSSN
 S LABARRAY("LAB_TEST")=LABTEST
 S LABARRAY("RESULT_DT")=LABDTTM
 S LABARRAY("RESULT_VAL")=DHPRSLT
 S LABARRAY("LOCATION")=DHPLOC
 S LABARRAY("LOINC")=DHPLOINC
 M RETSTA("LABDATA")=LABARRAY
 ;
 I $G(DEBUG)=1 D
 . W "ICN:      ",DHPPAT,!
 . W "SSN:      ",LABARRAY("PAT_SSN"),!
 . W "LOCATION: ",LABARRAY("LOCATION"),!
 . W "LOINC:    ",DHPLOINC,!
 . W "TEST:     ",LABARRAY("LAB_TEST"),!
 . W "RESULT:   ",LABARRAY("RESULT_VAL"),!
 . W "DATE:     ",LABARRAY("RESULT_DT"),!
 ;
 ;call ISI Labs Ingest
 S DHPRC=$$LAB^ISIIMP12(.RC,.LABARRAY)
 I +DHPRC=-1 S RETSTA=DHPRC QUIT
 ;
 S RETSTA=1
 ;
 QUIT
 ;
T1 ; test one
 ;
 M PARMS=^XTMP("SYNGRAPH",1,1267,"load","labs",13,"parms")
 S N=""
 F  S N=$O(PARMS(N)) Q:N=""  S @N=PARMS(N)
 D LABADD(.ZXC,DHPPAT,DHPLOC,DHPLAB,DHPOBS,DHPDTM,DHPLOINC)
 Q

SYNFLAB
SYNFLAB ;ven/gpl - fhir loader utilities ;Aug 15, 2019@14:20:15
 ;;0.2;VISTA SYN DATA LOADER;;Feb 07, 2019;Build 10
 ;
 ; Authored by George P. Lilly 2017-2018
 ;
 q
 ;
importLabs(rtn,ien,args) ; entry point for loading labs for a patient
 ; calls the intake Labs web service directly
 ;
 n grtn
 n root s root=$$setroot^SYNWD("fhir-intake")
 n % s %=$$wsIntakeLabs(.args,,.grtn,ien)
 ;i $d(grtn) d  ; something was returned
 ;. k @root@(ien,"load","labs")
 ;. m @root@(ien,"load","labs")=grtn("labs")
 ;. if $g(args("debug"))=1 m rtn=grtn
 if $g(args("debug"))=1 m rtn=grtn
 s rtn("labsStatus","status")=$g(grtn("status","status"))
 s rtn("labsStatus","loaded")=$g(grtn("status","loaded"))
 s rtn("labsStatus","errors")=$g(grtn("status","errors"))
 ;b
 ;
 ;
 q
 ;
wsIntakeLabs(args,body,result,ien) ; web service entry (post)
 ; for intake of one or more Lab results. input are fhir resources
 ; result is json and summarizes what was done
 ; args include patientId
 ; ien is specified for internal calls, where the json is already in a graph
 n root,troot
 s root=$$setroot^SYNWD("fhir-intake")
 ;
 n jtmp,json,jrslt,eval
 ;i $g(ien)'="" if $$loadStatus("labs","",ien)=1 d  q  ;
 ;. s result("labsStatus","status")="alreadyLoaded"
 i $g(ien)'="" d  ; internal call
 . s troot=$na(@root@(ien,"type","Observation"))
 . s eval=$na(@root@(ien,"load")) ; move eval to the graph
 . ;d getIntakeFhir^SYNFHIR("json",,"Observation",ien,1)
 e  q 0  ; sending not decoded json in BODY to this routine is not done
 ; todo: locate the patient and add the labs in BODY to the graph
 ;. ;s args("load")=0
 ;. merge jtmp=BODY
 ;. do decode^SYNJSONE("jtmp","json")
 ;. s troot=$na(@root@(ien,"type","Observation"))
 i '$d(@troot) q 0  ;
 s json=$na(@root@(ien,"json"))
 ;m ^gpl("gjson")=@troot
 ;
 ; determine the patient
 ;
 n dfn
 if $g(ien)'="" d  ;
 . s dfn=$$ien2dfn^SYNFUTL(ien) ; look up dfn in the graph
 else  d  ;
 . s dfn=$g(args("dfn"))
 . i dfn="" d  ;
 . . n icn s icn=$g(args("icn"))
 . . i icn'="" s dfn=$$icn2dfn^SYNFUTL(icn)
 i $g(dfn)="" do  quit 0  ; need the patient
 . s result("labs",1,"log",1)="Error, patient not found.. terminating"
 ;
 ;
 new zi s zi=0
 for  set zi=$order(@troot@(zi)) quit:+zi=0  do  ;
 . ;
 . ; define a place to log the processing of this entry
 . ;
 . new jlog set jlog=$name(@eval@("labs",zi))
 . ;
 . ; insure that the resourceType is Observation
 . ;
 . new type set type=$get(@json@("entry",zi,"resource","resourceType"))
 . if type'="Observation" do  quit  ;
 . . set @eval@("labs",zi,"vars","resourceType")=type
 . . do log(jlog,"Resource type not Observation, skipping entry")
 . set @eval@("labs",zi,"vars","resourceType")=type
 . ;
 . ; determine the Observation category and quit if not labs
 . ;
 . new obstype set obstype=$get(@json@("entry",zi,"resource","category",1,"coding",1,"code"))
 . if obstype="" do  ; category is missing, try mapping the code
 . . new trycode,trydisp,tryy
 . . set trycode=$g(@json@("entry",zi,"resource","code","coding",1,"code"))
 . . set trydisp=$g(@json@("entry",zi,"resource","code","coding",1,"display"))
 . . s tryy=$$loinc2sct(trycode)
 . . if tryy="" d  quit  ;
 . . . d log(jlog,"Observation Category missing, not vital signs; code is: "_trycode_" "_trydisp)
 . . if tryy'="" set obstype="laboratory"
 . . d log(jlog,"Derived category is "_obstype)
 . ;
 . if obstype'="laboratory" do  quit  ;
 . . set @eval@("labs",zi,"vars","observationCategory")=obstype
 . . do log(jlog,"Observation Category is not laboratory, skipping")
 . set @eval@("labs",zi,"vars","observationCategory")=obstype
 . ;
 . ; see if this resource has already been loaded. if so, skip it
 . ;
 . if $g(ien)'="" if $$loadStatus("labs",zi,ien)=1 do  quit  ;
 . . d log(jlog,"Lab already loaded, skipping")
 . ;
 . ; determine Labs type, code, coding system, and display text
 . ;
 . new labtype set labtype=$get(@json@("entry",zi,"resource","code","text"))
 . if labtype="" set labtype=$get(@json@("entry",zi,"resource","code","coding",1,"display"))
 . do log(jlog,"Labs type is: "_labtype)
 . set @eval@("labs",zi,"vars","type")=labtype
 . ;
 . ; determine the id of the resource
 . ;
 . new id set id=$get(@json@("entry",zi,"resource","id"))
 . set @eval@("labs",zi,"vars","id")=id
 . d log(jlog,"ID is: "_id)
 . ;
 . new obscode set obscode=$get(@json@("entry",zi,"resource","code","coding",1,"code"))
 . do log(jlog,"code is: "_obscode)
 . set @eval@("labs",zi,"vars","code")=obscode
 . ;
 . s ^gpl("labs",obscode,labtype)=""
 . ;
 . new codesystem set codesystem=$get(@json@("entry",zi,"resource","code","coding",1,"system"))
 . do log(jlog,"code system is: "_codesystem)
 . set @eval@("labs",zi,"vars","codeSystem")=codesystem
 . ;
 . ; determine the value and units
 . ;
 . new value set value=$get(@json@("entry",zi,"resource","valueQuantity","value"))
 . do log(jlog,"value is: "_value)
 . set @eval@("labs",zi,"vars","value")=value
 . ;
 . new unit set unit=$get(@json@("entry",zi,"resource","valueQuantity","unit"))
 . do log(jlog,"units are: "_unit)
 . set @eval@("labs",zi,"vars","units")=unit
 . ;
 . ; determine the effective date
 . ;
 . new effdate set effdate=$get(@json@("entry",zi,"resource","effectiveDateTime"))
 . do log(jlog,"effectiveDateTime is: "_effdate)
 . set @eval@("labs",zi,"vars","effectiveDateTime")=effdate
 . new fmtime s fmtime=$$fhirTfm^SYNFUTL(effdate)
 . d log(jlog,"fileman dateTime is: "_fmtime)
 . set @eval@("labs",zi,"vars","fmDateTime")=fmtime ;
 . new hl7time s hl7time=$$fhirThl7^SYNFUTL(effdate)
 . d log(jlog,"hl7 dateTime is: "_hl7time)
 . set @eval@("labs",zi,"vars","hl7DateTime")=hl7time ;
 . ;
 . ; set up to call the data loader
 . ;
 . n RETSTA,DHPPAT,DHPSCT,DHPOBS,DHPUNT,DHPDTM,DHPPROV,DHPLOC,DHPLOINC
 . ;
 . s DHPPAT=$$dfn2icn^SYNFUTL(dfn)
 . s @eval@("labs",zi,"parms","DHPPAT")=DHPPAT
 . ;
 . ;n vistalab s vistalab=$$MAP^SYNQLDM(obscode)
 . s DHPLOINC=obscode
 . d log(jlog,"LOINC code is: "_DHPLOINC)
 . s @eval@("labs",zi,"parms","DHPLOINC")=DHPLOINC
 . ;
 . n vistalab s vistalab=$$graphmap^SYNGRAPH("loinc-lab-map",obscode)
 . i +vistalab=-1 s vistalab=$$graphmap^SYNGRAPH("loinc-lab-map"," "_obscode)
 . i +vistalab=-1 s vistalab=$$covid^SYNGRAPH(obscode)
 . i +vistalab'=-1 d
 .. d log(jlog,"Lab found in graph: "_vistalab)
 .. s @eval@("labs",zi,"parms","vistalab")=vistalab
 . if +vistalab=-1 s vistalab=labtype
 . s vistalab=$$TRIM^XLFSTR(vistalab) ; get rid of trailing blanks
 . ;n sct s sct=$$loinc2sct(obscode) ; find the snomed code
 . ;i vistalab="" d  quit
 . ;. d log(jlog,"VistA lab not found for loinc code: "_obscode_" "_labtype_" -- skipping")
 . ;. s @eval@("labs",zi,"status","loadstatus")="cannotLoad"
 . ;. s @eval@("labs",zi,"status","issue")="VistA lab not found for loinc code: "_obscode_" "_labtype_" -- skipping"
 . ;. s @eval@("status","errors")=$g(@eval@("status","errors"))+1
 . s @eval@("labs",zi,"parms","DHPLAB")=vistalab
 . d log(jlog,"VistA Lab is: "_vistalab)
 . s DHPLAB=vistalab
 . ;
 . s DHPOBS=value
 . s recien=$o(^LAB(60,"B",DHPLAB,""))
 . n xform s xform=$$GET1^DIQ(60,recien_",",410)
 . n dec s dec=0
 . i xform["S Q9=" d
 . . s dec=+$p($p(xform,"""",2),",",3)
 . i $l($p(DHPOBS,".",2))>1 d
 . . s DHPOBS=$s(dec<4:$j(DHPOBS,1,dec),dec>3:$j(DHPOBS,1,3),1:$j(DHPOBS,1,0)) ; fix results with too many decimal places
 . ; added for Covid tests
 . i DHPOBS="" d  ; no quant value
 . . n vtxt ; value text
 . . s vtxt=$get(@json@("entry",zi,"resource","valueCodeableConcept","text"))
 . . i vtxt["Negative" s DHPOBS="Negative" ; 260385009
 . . i vtxt["Positive" s DHPOBS="Positive" ; 10828004
 . . i vtxt["Detected" s DHPOBS="Detected" ; 260373001
 . . i vtxt["Not detected" s DHPOBS="Not detected" ; 260415000
 . . i vtxt["Confirmed" s DHPOBS="Confirmed" ; 
 . s @eval@("labs",zi,"parms","DHPOBS")=DHPOBS
 . d log(jlog,"Value is: "_DHPOBS)
 . ;
 . ;i DHPLOINC="2093-3" s DHPOBS=$J(DHPOBS,1,0) ;Total Cholesterol
 . ;i DHPLOINC="33914-3" s DHPOBS=$J(DHPOBS,1,0) ;Estimated Glomerular Filtration Rate
 . ;i DHPLOINC="18262-6" s DHPOBS=$J(DHPOBS,1,0) ;Low Density Cholesterol
 . ;i DHPLOINC="2085-9" s DHPOBS=$J(DHPOBS,1,0) ;High Density Cholesterol
 . ;i DHPLOINC="2571-8" s DHPOBS=$J(DHPOBS,1,0) ;Tryglycerides
 . ;i DHPLOINC="2339-0" s DHPOBS=$J(DHPOBS,1,0) ;Glucose
 . ;
 . s DHPUNT=unit
 . s @eval@("labs",zi,"parms","DHPUNT")=unit
 . d log(jlog,"Units are: "_unit)
 . ;
 . s DHPDTM=hl7time
 . s @eval@("labs",zi,"parms","DHPDTM")=hl7time
 . d log(jlog,"HL7 DateTime is: "_hl7time)
 . ;
 . s DHPPROV=$$MAP^SYNQLDM("OP","provider")
 . n DHPPROVIEN s DHPPROVIEN=$o(^VA(200,"B",DHPPROV,""))
 . if DHPPROVIEN="" S DHPPROVIEN=3
 . s @eval@("labs",zi,"parms","DHPPROV")=DHPPROVIEN
 . d log(jlog,"Provider for outpatient is: #"_DHPPROVIEN_" "_DHPPROV)
 . ;
 . s DHPLOC=$$MAP^SYNQLDM("OP","location")
 . n DHPLOCIEN s DHPLOCIEN=$o(^SC("B",DHPLOC,""))
 . if DHPLOCIEN="" S DHPLOCIEN=4
 . s @eval@("labs",zi,"parms","DHPLOC")=DHPLOC
 . d log(jlog,"Location for outpatient is: #"_DHPLOCIEN_" "_DHPLOC)
 . ;
 . s @eval@("labs",zi,"status","loadstatus")="readyToLoad"
 . ;
 . if $g(args("load"))=1 d  ; only load if told to
 . . ;new (DHPPAT,DHPSCT,DHPOBS,DHPUNT,DHPDTM,DHPPROV,DHPLOC,DHPLOINC,DHPLAB)
 . . if $g(ien)'="" if $$loadStatus("labs",zi,ien)=1 do  quit  ;
 . . . d log(jlog,"Lab already loaded, skipping")
 . . d log(jlog,"Calling LABADD^SYNDHP63 to add lab")
 . . ;new (DHPPAT,DHPSCT,DHPOBS,DHPUNT,DHPDTM,DHPPROV,DHPLOC,DHPLOINC,DHPLAB,DUZ,DT,U,jlog,ien,zi,eval)
 . . ;LABADD(RETSTA,DHPPAT,DHPLOC,DHPTEST,DHPRSLT,DHPRSDT) ;Create lab test
 . . D LABADD^SYNDHP63(.RETSTA,DHPPAT,DHPLOC,DHPLAB,DHPOBS,DHPDTM,DHPLOINC)     ; labs update
 . . d log(jlog,"Return from LABADD^ZZDHP63 was: "_$g(RETSTA))
 . . i $g(DEBUG)=1 ZWRITE RETSTA
 . . if +$g(RETSTA)=1 do  ;
 . . . s @eval@("labs","status","loaded")=$g(@eval@("labs","status","loaded"))+1
 . . . s @eval@("labs",zi,"status","loadstatus")="loaded"
 . . else  s @eval@("labs","status","errors")=$g(@eval@("labs","status","errors"))+1
 ;
 if $get(args("debug"))=1 do  ;
 . m jrslt("source")=@json
 . m jrslt("args")=args
 . m jrslt("eval")=@eval
 m jrslt("labsStatus")=@eval@("labsStatus")
 set jrslt("result","status")="ok"
 set jrslt("result","loaded")=$g(@eval@("labs","status","loaded"))
 set jrslt("result","errors")=$g(@eval@("labs","status","errors"))
 i $g(ien)'="" d  ; called internally
 . ;m result=eval
 . m result("status")=jrslt("result")
 . ;b
 e  d  ;
 . d encode^SYNJSONE("jrslt","result")
 . set HTTPRSP("mime")="application/json"
 q 1
 ;
log(ary,txt) ; adds a text line to @ary@("log")
 s @ary@("log",$o(@ary@("log",""),-1)+1)=$g(txt)
 w:$G(DEBUG) !,"      ",$G(txt)
 q
 ;
loadStatus(typ,zx,zien) ; extrinsic return 1 if resource was loaded
 n root s root=$$setroot^SYNWD("fhir-intake")
 n rt s rt=0
 i $g(zx)="" i $d(@root@(zien,"load",typ)) s rt=1 q rt
 i $get(@root@(zien,"load",typ,zx,"status","loadstatus"))="loaded" s rt=1
 q rt
loinc2sct(loinc) ; extrinsic returns a Snomed code for a Loinc code
 ; for labs
 ; thanks to Ferdi for the Snomed mapping
 ;
 ; here's what we got so far:
 ;^gpl("labs","29463-7","Body Weight")=""
 ;^gpl("labs","39156-5","Body Mass Index")="" ; oops
 ;^gpl("labs","55284-4","Blood Pressure")=""
 ;^gpl("labs","8302-2","Body Height")=""
 ;^gpl("labs","8331-1","Oral temperature")="" ;
 ;
 S SCTA("29463-7",27113001)="9^Body weight"
 S SCTA("8302-2",50373000)="8^Body height"
 S SCTA("55284-4",75367002)="1^Blood pressure"
 S SCTA(78564009)="5^Pulse rate"
 S SCTA("8331-1",386725007)="2^Body Temperature"
 S SCTA(86290005)="3^Respiration"
 S SCTA(48094003)="10^Abdominal girth measurement"
 S SCTA(21727005)="11^Audiometry"
 S SCTA(252465000)="21^Pulse oximetry"
 S SCTA(22253000)="22^Pain"
 ;
 q $o(SCTA(loinc,""))
 ;
testall ; run the labs import on all imported patients
 new root s root=$$setroot^SYNWD("fhir-intake")
 new indx s indx=$na(@root@("POS","DFN"))
 n dfn,ien,filter,reslt
 s dfn=0
 f  s dfn=$o(@indx@(dfn)) q:+dfn=0  d  ;
 . s ien=$o(@indx@(dfn,""))
 . q:ien=""
 . s filter("dfn")=dfn
 . k reslt
 . d wsIntakeLabs(.filter,,.reslt,ien)
 q
 ;
labsum ; summary of lab tests for patient ien pien
 n root s root=$$setroot^SYNWD("fhir-intake")
 n table
 n zzi s zzi=0
 f  s zzi=$o(@root@(zzi)) q:+zzi=0  d  ;
 . n labs
 . d getIntakeFhir^SYNFHIR("labs",,"Observation",zzi,1)
 . n zi s zi=0
 . f  s zi=$o(labs("entry",zi)) q:+zi=0  d  ;
 . . n groot s groot=$na(labs("entry",zi,"resource"))
 . . i $g(@groot@("category",1,"coding",1,"code"))'="laboratory" q  ;
 . . n loinc
 . . s loinc=$g(@groot@("code","coding",1,"code"))
 . . q:loinc=""
 . . ;i loinc="6082-2" b  ;
 . . n text s text=$g(@groot@("code","coding",1,"display"))
 . . i $d(table(loinc_" "_text)) d  ;
 . . . s table(loinc_" "_text)=table(loinc_" "_text)+1
 . . e  d  ;
 . . . s table(loinc_" "_text)=1
 . . . w !,"patient= "_zzi_" entry= "_zi,!
 . . . n rptary m rptary=@root@(zzi,"json","entry",zi,"resource")
 . . . zwrite rptary
 zwrite table
 q
 ;

SYNGRAPH
SYNFGRAPH       ;ven/gpl - fhir loader utilities ;2018-08-17  3:26 PM
 ;;0.2;VISTA SYN DATA LOADER;;Feb 07, 2019;Build 10
 ;
 ; Authored by George P. Lilly 2017-2018
 ;
 q
 ;
index(graph) ; will create a pos and ops index at the place pointed to by graph
 ; graph is passed by name
 ;
 i $d(@graph@("pos")) k @graph@("pos")
 i $d(@graph@("ops")) k @graph@("ops")
 n zi s zi=0
 f  s zi=$o(@graph@(zi)) q:+zi=0  d  ;
 . n zj,zv
 . s zj=""
 . f  s zj=$o(@graph@(zi,zj)) q:zj=""  d  ;
 . . s zv=$g(@graph@(zi,zj))
 . . q:zv=""
 . . s @graph@("pos",zj,zv,zi)=""
 . . s @graph@("ops",zv,zj,zi)=""
 q
 ;
graphmap(graph,code,otype,itype) ; extrinsic mapping function using a graph
 ; graph is a pointer to the graph passed by name
 ; code is the subject to map
 ; otype is the predicate for the result
 ; itype is the predicate for the subject (optional)
 ;
 ;i '$d(@graph@("pos")) d  ;
 i $e(graph,1,1)'="^" d  ;
 . n root s root=$$setroot^SYNWD(graph)
 . s graph=$g(@root@("index","root"))
 i $g(graph)="" q "-1^Error, can't find graph"
 i '$d(@graph@("pos")) q "-1^Error, can't find index"
 i $g(code)="" q "-1^Error, code not provided"
 n gien,ipred,opred
 s ipred=$g(itype)
 i ipred="" s ipred=$o(@graph@("ops",code,""))
 ;w !,"ipred= "_ipred
 i ipred="" q "-1^Code not found"
 s gien=$o(@graph@("ops",code,ipred,""))
 ;w !,"gien= "_gien,!
 ;zwrite @graph@(gien,*)
 ;w !,graph
 s opred=$g(otype)
 i opred="" s opred=$o(@graph@(gien,ipred))
 i opred="" s opred=$o(@graph@(gien,ipred),-1)
 ;w !,"opred= "_opred
 q $g(@graph@(gien,opred))
 ;
 s gien=$o(@graph@("ops"))
 n ugraph s ugraph=""
 i '$d(@graph@("pos")) d  ; fix the pointer
 . i $d(@graph@("graph","pos")) s ugraph=$na(@graph@("graph"))
 . n graphname s graphname=$o(@graph@("graph",""))
 . i graphname="" s ugraph="" q  ; don't understand this graph
 . i $d(@graph@("graph",graphname,"pos")) s ugraph=$na(@graph@("graph",graphname))
 q
 ;
getGraphMap(rtn,graph,ipred,iobj,opred,oobj) ; retrieve a section of the graph
 ; extrinsic return 1 on success and -1 on failure
 ; identified by predicate ipred with object iobj
 ; if opred (output predicate) and/or oobj (output object) are specified,
 ;  they are treated and a "and" condition on the retrieval
 ;
 n root s root=$$setroot^SYNWD(graph)
 n groot s groot=$g(@root@("index","root"))
 i groot="" q "-1^can't find graph root"
 i '$d(@groot@("pos")) q "-1^can't find graph index"
 k @rtn
 ;
 n gien
 i $g(iobj)'="" i $o(@groot@("ops",iobj,""))'="" d  ;
 . i $g(ipred)'="" d  q  ;
 . . s gien=$o(@groot@("pos",ipred,iobj,""))
 . . m @rtn=@groot@(gien)
 . n tpred
 . s tpred=$o(@groot@("ops",iobj,""))
 . s gien=$o(@groot@("pos",tpred,iobj,""))
 . m @rtn=@groot@(gien)
 i $d(@rtn) q 1
 q "-1^No Match"
 ;
getGraph(url,gname) ; get a graph from a web service
 ;
 q:'$d(gname)
 i $$rootOf^SYNWD(gname)'=-1 d  q  ;
 . w !,"error, graph exists: "_gname
 n root s root=$$setroot^SYNWD(gname)
 n %,json,grf
 s %=$$%^%WC(.json,"GET",url)
 w !,"result= ",%
 i '$d(json) d  q  ;
 . w !,"error, nothing returned"
 d decode^SYNJSONE("json","grf")
 m @root=grf
 n indx,rindx
 s indx=$o(@root@(0))
 s rindx=$na(@root@(indx))
 s @root@("index","root")=rindx
 d index(rindx)
 q
 ;
wsGetGraph(RTN,FILTER) ; web service returns the requested graph FILTER("graph")="graph"
 ; this is the server side of getGraph above
 n graph s graph=$g(FILTER("graph"))
 i graph="" d  q  ;
 . s RTN="-1^please specify a graph"
 ;n root s root=$$rootOf^SYNWD(graph)
 n root s root=$$setroot^SYNWD(graph)
 i +root=-1 d  q  ;
 . s RTN="-1^graph not found"
 ;
 ;n json
 ;s json=$$setroot^SYNWD(graph_"-json")
 ;i +json'=-1 s RTN=json q  ;
 ;s json=$$setroot^SYNWD(graph_"-json")
 S RTN=$na(^TMP("SYNOUT",$J))
 K @RTN
 d encode^SYNJSONE(root,RTN)
 q
 ;
loincMap() ; create the lonic-lab-map
 n root s root=$$setroot^SYNWD("loinc-lab-map")
 k @root
 n src s src=$$rootOf^SYNWD("loinc-code-map")
 s src=$na(@src@("loinc-codes-map-1.csv"))
 n zi s zi=0
 f  s zi=$o(@src@(zi)) q:+zi=0  d  ;
 . n zj s zj=""
 . f  s zj=$o(@src@(zi,zj)) q:zj=""  d  ;
 . . n zt s zt=$$TRIM^XLFSTR(@src@(zi,zj))
 . . q:zt=""
 . . s @root@("graph","map",zi,zj)=zt
 s rindx=$na(@root@("graph","map"))
 s @root@("index","root")=rindx
 d index(rindx)
 q
 ;
covid(loinc) ; extrinsic returns the name of the lab test  in ^LAB(60, 
 ; for the loinc code
 n rt,rien,LAB
 s LAB(5091)="COVID-19 (PHRL)"
 s LAB(5092)="COVID-19 CONFIRMATORY"
 s LAB("94531-1",5093)="COVID-19 PANEL"
 s LAB("92130-4",5096)="RHINOVIRUS RNA RESP"
 s LAB("92131-2",5097)="RESPSYNVIRUS RNA"
 s LAB("92134-6",5098)="METAPNEUMOVIRUS RNA"
 s LAB("92138-7",5099)="PARAINFLUENZA VIRUS 3 RNA"
 s LAB("92139-5",5100)="PARAINFLUENZA VIRUS 2 RNA"
 s LAB("92140-3",5101)="PARAINFLUENZA VIRUS 1 RNA"
 s LAB("92141-1",5103)="INFLUENZA B RNA"
 s LAB("80383-3",5103)="INFLUENZA B RNA"
 s LAB("92142-9",5104)="INFLUENZA A RNA"
 s LAB("80382-5",5104)="INFLUENZA A RNA"
 s LAB("94040-3",5105)="ADENOVIRUS A+B+C+D+E DNA"
 s rien=$o(LAB(loinc,""))
 s rt=$g(LAB(loinc,rien))
 i rt="" s rt=-1
 q rt
 ;



